%{
	#include "hw3_output.hpp"
    #include "symbol_table.hpp"
    #include "types.hpp"
	#include <iostream>
	#include <stdlib.h>
    using namespace output;

	extern int yylex();
    extern int yylineno;
	int yyerror(const char * message);

    TableStack* tables = new TableStack();
%}

%token VOID INT BYTE B BOOL OVERRIDE TRUE FALSE RETURN IF WHILE BREAK CONTINUE COMMA LBRACE RBRACE ID NUM STRING
%right ASSIGN
%left OR
%left AND
%left RELOP
%left PLUSMINUS
%left MULTDIV
%right NOT
%left LPAREN
%left RPAREN
%left SC
%right ELSE

%%

Program     :   Funcs { 
                    if(!tables->containsMain()) {
                        errorMainMissing();
                        exit(1);
                    }
                    tables->closeScope();
                    exit(0);
                };
Funcs       :         { }
            |   FuncDecl Funcs { }
            ;
FuncDecl    :   OverRide RetType ID LPAREN Formals {
                    OverrideNode* override = (OverrideNode*)$1;
                    RetTypeNode* ret_type = (RetTypeNode*)$2;
                    IdNode* id = (IdNode*)$3;
                    FormalsNode* formals = (FormalsNode*)$5;

                    vector<string> type;
                    type.push_back(ret_type->GetType());
                    vector<FormalDeclNode*> vec = formals->GetList();
                    for(int i = 0; i < vec.size(); i++) {
                        type.push_back(vec[i]->GetType());
                    }
                    int res = tables->Insert(id->GetName(), type, 0, override->CheckIsOverride(), true);
                    switch(res) {
                        case ERROR_DEF:
                            errorDef(yylineno, id->GetName());
                            break;
                        case ERROR_NO_OVERRIDE:
                            errorFuncNoOverride(yylineno, id->GetName());
                            break;
                        case ERROR_OVERRIDE_NO_DECL:
                            errorOverrideWithoutDeclaration(yylineno, id->GetName());
                            break;
                        case ERROR_MAIN_OVERRIDE:
                            errorMainOverride(yylineno);
                            break;
                    }

                    if(res != SUCCESS) exit(1);
                    // should add $$ = new FuncDeclNode* ?

                } RPAREN LBRACE Statements RBRACE {
                    tables->funcType = "";
                    tables->closeScope();    
                };
OverRide    :         { $$ = new OverrideNode(); }
            |   OVERRIDE { $$ = new OverrideNode(true); }
            ;
RetType     :   Type { 
                    TypeNode* type = (TypeNode*)$1;
                    $$ = new RetTypeNode(type->GetType());
                }
            |   VOID { 
                    $$ = new RetTypeNode("void");
                }
            ;
Formals     :        { $$ = new FormalsNode(); }
            |   FormalsList { $$ = new FormalsNode((FormalsListNode*)$1); }
            ;
FormalsList :   FormalDecl { 
                    vector<FormalDeclNode*> vec;
                    vec.insert(vec.begin(), (FormalDeclNode*)$1);
                    $$ = new FormalsListNode(vec);
                }
            |   FormalDecl COMMA FormalsList { 
                    FormalDeclNode* decl = (FormalDeclNode*)$1;
                    FormalsListNode* list = (FormalsListNode*)$3;
                    vector<FormalDeclNode*> vec = list->GetDecls();
                    vec.insert(vec.begin(), decl);
                    $$ = new FormalsListNode(vec);
                }
            ;
FormalDecl  :   Type ID { 
                    TypeNode* type = (TypeNode*)$1;
                    IdNode* id = (IdNode*)$2;
                    if(tables->isInCurrentScope(id->GetName())) {
                        errorDef(yylineno, id->GetName());
                        exit(1);
                    }
                    $$ = new FormalDeclNode(type->GetType(), id->GetName());
                };
Statements  :   Statement { }
            |   Statements Statement { }
            ;
Statement   :   LBRACE OPENSCOPE Statements RBRACE CLOSESCOPE { }
            |   Type ID SC { 
                    TypeNode* type = (TypeNode*)$1;
                    IdNode* id = (IdNode*)$2;
                    int res = tables->Insert(id->GetName(), { type->GetType() }, tables->offsets.top());
                    if(res == ERROR_DEF) {
                        errorDef(yylineno, id->GetName());
                        exit(1);
                    }
                }
            |   Type ID ASSIGN Exp SC { 
                    TypeNode* type = (TypeNode*)$1;
                    IdNode* id = (IdNode*)$2;
                    ExpNode* exp = (ExpNode*)$4;
                    if(isLegalAssign(type->GetType(), exp->GetType())) {
                        int res = tables->Insert(id->GetName(), { type->GetType() }, tables->offsets.top());
                        if(res == ERROR_DEF) {
                            errorDef(yylineno, id->GetName());
                            exit(1);
                        }
                    }
                    else {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                }
            |   ID ASSIGN Exp SC { 
                    IdNode* id = (IdNode*)$1;
                    ExpNode* exp = (ExpNode*)$3;
                    int res = tables->containsSymbol(id->GetName(), { exp->GetType() });
                    if(res == ERROR_UNDEF) {
                        errorUndef(yylineno, id->GetName());
                        exit(1);
                    }
                    else {
                        string type = tables->getType(id->GetName());
                        if(!isLegalAssign(type, exp->GetType())) {
                            errorMismatch(yylineno);
                            exit(1);
                        }
                    }
                }
            |   Call SC { }
            |   RETURN SC { 
                    if(tables->funcType != "void") {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                }
            |   RETURN Exp SC { 
                    ExpNode* exp = (ExpNode*)$2;
                    if(!isLegalAssign(tables->funcType, exp->GetType())) {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                }
            |   IF LPAREN ISEXPBOOL RPAREN OPENSCOPE Statement CLOSESCOPE { }
            |   IF LPAREN ISEXPBOOL RPAREN OPENSCOPE Statement CLOSESCOPE ELSE OPENSCOPE Statement CLOSESCOPE { }
            |   WHILE LPAREN ISEXPBOOL RPAREN WHILESCOPE Statement CLOSESCOPE { }
            |   BREAK SC { 
                    if(!tables->isCurrentScopeWhile()) {
                        errorUnexpectedBreak(yylineno);
                        exit(1);
                    }
                }
            |   CONTINUE SC {
                    if(!tables->isCurrentScopeWhile()) {
                        errorUnexpectedContinue(yylineno);
                        exit(1);
                    }
                }
            ;
Call        :   ID LPAREN ExpList RPAREN {
                    IdNode* id = (IdNode*)$1;
                    ExpListNode* exp_list = (ExpListNode*)$3;
                    vector<string> types;
                    vector<ExpNode*> list = exp_list->GetExpsList();
                    for(int i = 0; i < list.size(); i++) {
                        types.push_back(list[i]->GetType());
                    }
                    int res = tables->containsSymbol(id->GetName(), types, true);
                    switch(res) {
                        case ERROR_UNDEF_FUNC:
                            errorUndefFunc(yylineno, id->GetName());
                            exit(1);
                            break;
                        case ERROR_PROTOTYPE_MISMATCH:
                            errorPrototypeMismatch(yylineno, id->GetName());
                            exit(1);
                            break;
                        case ERROR_AMBIGUOUS_CALL:
                            errorAmbiguousCall(yylineno, id->GetName());
                            exit(1);
                            break;
                    }
                    if(res != SUCCESS) exit(1);
                    $$ = new CallNode(tables->getType(id->GetName()), id->GetName());
                }
            |   ID LPAREN RPAREN { 
                    IdNode* id = (IdNode*)$1;
                    int res = tables->containsSymbol(id->GetName(), {}, true);
                    switch(res) {
                        case ERROR_UNDEF_FUNC:
                            errorUndefFunc(yylineno, id->GetName());
                            exit(1);
                            break;
                    }
                    if(res != SUCCESS) exit(1);
                    $$ = new CallNode(tables->getType(id->GetName()), id->GetName());
                }
            ;
ExpList     :   Exp { 
                    vector<ExpNode*> exps;
                    exps.push_back((ExpNode*)$1);
                    $$ = new ExpListNode(exps);
                }
            |   Exp COMMA ExpList { 
                    ExpListNode* list = (ExpListNode*)$3;
                    list->GetExpsList().push_back((ExpNode*)$1);
                    $$ = new ExpListNode(list->GetExpsList());
                }
            ;
Type        :   INT { $$ = new TypeNode("int"); }
            |   BYTE { $$ = new TypeNode("byte"); }
            |   BOOL { $$ = new TypeNode("bool"); }
            ;
Exp         :   LPAREN Exp RPAREN { $$ = new ExpNode(((ExpNode*)$2)->GetType()); }
            |   Exp PLUSMINUS Exp { 
                    ExpNode* exp1 = (ExpNode*)$1;
                    ExpNode* exp2 = (ExpNode*)$3;
                    string type = GetSumType(exp1->GetType(), exp2->GetType());
                    if(type == "") {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                    $$ = new ExpNode(type);
                }
            |   Exp MULTDIV Exp { 
                    ExpNode* exp1 = (ExpNode*)$1;
                    ExpNode* exp2 = (ExpNode*)$3;
                    string type = GetSumType(exp1->GetType(), exp2->GetType());
                    if(type == "") {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                    $$ = new ExpNode(type);
                }
            |   ID { 
                    IdNode* id = (IdNode*)$1;
                    string type = tables->getType(id->GetName());
                    if(type == "") {
                        errorUndef(yylineno, id->GetName());
                        exit(1);
                    }
                    $$ = new ExpNode(type);
                }
            |   Call { $$ = new ExpNode(((CallNode*)$1)->GetType()); }
            |   NUM { $$ = new ExpNode("int"); }
            |   NUM B { 
                    NumNode* num = (NumNode*)$1;
                    if(stoi(num->GetNumAsString()) > 255) {
                        errorByteTooLarge(yylineno, num->GetNumAsString());
                        exit(1);
                    }
                    $$ = new ExpNode("byte");
                }
            |   STRING { $$ = new ExpNode("string"); }
            |   TRUE { $$ = new ExpNode("bool"); }
            |   FALSE { $$ = new ExpNode("bool"); }
            |   NOT Exp { 
                    ExpNode* exp = (ExpNode*)$2;
                    if(exp->GetType() != "bool") {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                    $$ = new ExpNode("bool");
                }
            |   Exp AND Exp { 
                    ExpNode* exp1 = (ExpNode*)$1;
                    ExpNode* exp2 = (ExpNode*)$3;
                    if(exp1->GetType() != "bool" || exp2->GetType() != "bool") {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                    $$ = new ExpNode("bool");
                }
            |   Exp OR Exp { 
                    ExpNode* exp1 = (ExpNode*)$1;
                    ExpNode* exp2 = (ExpNode*)$3;
                    if(exp1->GetType() != "bool" || exp2->GetType() != "bool") {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                    $$ = new ExpNode("bool");
                }
            |   Exp RELOP Exp {
                    ExpNode* exp1 = (ExpNode*)$1;
                    ExpNode* exp2 = (ExpNode*)$3;
                    if(GetSumType(exp1->GetType(), exp2->GetType()) == "") {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                    $$ = new ExpNode("bool");
                }
            |   LPAREN Type RPAREN Exp {
                    TypeNode* type = (TypeNode*)$2;
                    ExpNode* exp = (ExpNode*)$4;
                    if(!IsLegalConvertion(exp->GetType() ,type->GetType())) {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                    $$ = new ExpNode(type->GetType());
                }
            ;

ISEXPBOOL   :   Exp {
                    ExpNode* exp = (ExpNode*)$1;
                    if(exp->GetType() != "bool") {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                };
OPENSCOPE   :   { tables->newScope(); };
WHILESCOPE  :   { tables->newScope(true); };
CLOSESCOPE  :   { tables->closeScope(); };

%%

int main()
{
	int res = yyparse();
    delete tables;
    return res;
}

int yyerror(const char * message)
{
	errorSyn(yylineno);
	exit(0);
}
